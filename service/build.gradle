plugins {
	id 'idea'
	id 'jacoco'
	id 'java'

	id 'org.springframework.boot'

	id 'com.diffplug.spotless'
	id 'com.github.ben-manes.versions' version '0.42.0'
	id 'com.github.spotbugs' version '5.0.6'
	id 'com.google.cloud.tools.jib'
	id 'com.gorylenko.gradle-git-properties' version '2.4.0'
	id 'io.freefair.lombok'
	id 'org.hidetake.swagger.generator'
  id 'com.srcclr.gradle' version '3.1.12'
	id 'org.sonarqube'
}

repositories {
	mavenCentral()
	maven { url 'https://broadinstitute.jfrog.io/artifactory/libs-snapshot' }
	maven { url 'https://broadinstitute.jfrog.io/artifactory/libs-release' }
}


description = 'DRS Hub'

apply from: 'publishing.gradle'
apply from: 'spring.gradle'
apply from: 'generators.gradle'
apply from: 'tooling.gradle'
apply from: 'analysis.gradle'

dependencyManagement {
	dependencies {
		dependencySet(group:'io.github.hakky54', version: '8.2.0') {
			entry 'sslcontext-kickstart-for-apache5'
			entry 'sslcontext-kickstart-for-pem'
		}
	}
}

dependencies {
	annotationProcessor 'org.immutables:value:2.9.2'
	implementation 'io.github.hakky54:sslcontext-kickstart-for-apache5'
	implementation 'io.github.hakky54:sslcontext-kickstart-for-pem'

	implementation('bio.terra:terra-common-lib:0.1.9-SNAPSHOT') {
		// remove transitive database deps, drshub has no db
		exclude group: 'org.liquibase', module: 'liquibase-core'
		exclude group: 'bio.terra', module: 'stairway-gcp'
		exclude group: 'bio.terra', module: 'stairway'
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-data-jdbc'
		exclude group: 'com.google.oauth-client', module: 'google-oauth-client'
	}
	implementation 'org.broadinstitute.dsde.workbench:sam-client_2.13:0.1-6a32c36'
	implementation 'bio.terra:externalcreds-client-resttemplate-jakarta:0.112.0-SNAPSHOT'
	implementation 'io.sentry:sentry-logback:6.19.0'
	implementation 'org.codehaus.janino:janino:3.1.9' // Provides if/else xml parsing for logback config

	// google
	implementation platform('com.google.cloud:libraries-bom:26.28.0')
	implementation 'com.google.oauth-client:google-oauth-client'
	implementation 'com.google.cloud:google-cloud-storage'
	implementation 'com.google.cloud:spring-cloud-gcp-starter-logging:4.9.0'

	implementation 'io.sentry:sentry-spring-boot-starter:6.18.1'

	// For Micrometer metrics gathering
	implementation 'io.micrometer:micrometer-registry-prometheus'

	// For OpenTelemetry
	var openTelemetryVersion = '1.31.0'
	implementation "io.opentelemetry:opentelemetry-api:${openTelemetryVersion}"
	implementation "io.opentelemetry:opentelemetry-sdk:${openTelemetryVersion}"
	implementation "io.opentelemetry:opentelemetry-sdk-metrics:${openTelemetryVersion}"
	implementation "io.opentelemetry:opentelemetry-exporter-logging:${openTelemetryVersion}"
	implementation "io.opentelemetry.semconv:opentelemetry-semconv:1.21.0-alpha"
	implementation "io.opentelemetry.instrumentation:opentelemetry-spring-webmvc-6.0:${openTelemetryVersion}-alpha"
	implementation "io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations:${openTelemetryVersion}"
	implementation "io.opentelemetry.instrumentation:opentelemetry-spring-boot:${openTelemetryVersion}-alpha"
	implementation "com.google.cloud.opentelemetry:exporter-trace:0.25.2"

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		// Fixes warning about multiple occurrences of JSONObject on the classpath
		exclude group: 'com.vaadin.external.google', module: 'android-json'
	}

	testImplementation('au.com.dius.pact.provider:junit5:4.5.3')
	testImplementation('au.com.dius.pact.provider:junit5spring:4.5.3')
	testImplementation('org.awaitility:awaitility:4.2.0')
}

sonarqube {
	properties {
		property "sonar.projectName", "terra-drs-hub"
		property "sonar.projectKey", "DataBiosphere_terra-drs-hub"
		property "sonar.organization", "broad-databiosphere"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.sources", "src/main/java,src/main/resources/templates"
	}
}

