plugins {
	id 'idea'
	id 'jacoco'
	id 'java'

	id 'org.springframework.boot'

	id 'com.diffplug.spotless'
	id 'com.github.ben-manes.versions' version '0.42.0'
	id 'com.github.spotbugs' version '5.0.6'
	id 'com.google.cloud.tools.jib'
	id 'com.gorylenko.gradle-git-properties' version '2.4.0'
	id 'io.freefair.lombok'
	id 'org.hidetake.swagger.generator'
}

repositories {
	mavenCentral()
	maven { url 'https://broadinstitute.jfrog.io/artifactory/libs-snapshot' }
	maven { url 'https://broadinstitute.jfrog.io/artifactory/libs-release' }
}


description = 'DRS Hub'

apply from: 'publishing.gradle'
apply from: 'spring.gradle'
apply from: 'generators.gradle'
apply from: 'tooling.gradle'
apply from: 'analysis.gradle'

dependencyManagement {
	dependencies {
		dependencySet(group:'io.github.hakky54', version: '7.3.0') {
			entry 'sslcontext-kickstart-for-apache4'
			entry 'sslcontext-kickstart-for-pem'
		}
	}
}

dependencies {
	annotationProcessor 'org.immutables:value:2.9.0'
	implementation 'io.github.hakky54:sslcontext-kickstart-for-apache4'
	implementation 'io.github.hakky54:sslcontext-kickstart-for-pem'

	implementation('bio.terra:terra-common-lib:0.0.60-SNAPSHOT') {
		// remove transitive database deps, drshub has no db
		exclude group: 'org.liquibase', module: 'liquibase-core'
		exclude group: 'bio.terra', module: 'stairway-gcp'
		exclude group: 'bio.terra', module: 'stairway'
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-data-jdbc'
	}
	implementation 'org.broadinstitute.dsde.workbench:sam-client_2.13:0.1-1b67d8b'
	implementation 'bio.terra:externalcreds-client-resttemplate:0.72.0-SNAPSHOT'
	implementation 'io.sentry:sentry-logback:6.1.4'
	implementation 'org.codehaus.janino:janino:3.1.7' // Provides if/else xml parsing for logback config
	implementation('org.springframework.cloud:spring-cloud-gcp-starter-logging:1.2.8.RELEASE') {
		exclude group: 'org.springframework', module: 'spring-jcl'
	}
}
