plugins {
	id 'idea'

	id 'io.spring.dependency-management' version '1.1.4' apply false
	id 'org.springframework.boot' version '3.1.7' apply false
	id 'com.diffplug.spotless' version '6.3.0' apply false
	// ^^ for some reason, can't use spotless in multiple subprojects without this ^^
	id 'com.google.cloud.tools.jib' version '3.1.4' apply false
	// ^^ see https://github.com/GoogleContainerTools/jib/blob/master/docs/faq.md#i-am-seeing-method-not-found-or-class-not-found-errors-when-building
	id 'io.freefair.lombok' version '6.4.1' apply false
	id 'org.hidetake.swagger.generator' version '2.19.2' apply false
	id 'org.sonarqube' version '3.3' apply false
}

repositories { mavenCentral() }

subprojects {
	group = 'bio.terra'
	version = '1.3.0-SNAPSHOT'

	ext {
		artifactGroup = "${group}.${rootProject.name}"
	}

	apply plugin: 'io.spring.dependency-management'

	dependencyManagement {
		imports {
			mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
		}

		dependencies {
			dependency 'io.swagger.core.v3:swagger-annotations:2.2.12'
			dependency 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.47'
		}
	}

	tasks.withType(org.hidetake.gradle.swagger.generator.GenerateSwaggerCode).all {
		jvmArgs = ['--add-opens=java.base/java.util=ALL-UNNAMED'] // for jvm 16+
	}
}

idea.project.jdkName = '17'
